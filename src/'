

/* The bootloader will look at this image and start execution at the symbol
   designated as the entry point. */
ENTRY(_start)

INCLUDE "os_defs.ld"

/*
 * I'm sick of always doing these calcs.
 *
 * 0x          0 =   0  B
 * 0x          1 =   1  B
 * 0x         10 =  16  B
 * 0x        100 = 256  B
 * 0x       1000 =   4 KB
 * 0x     1_0000 =  64 KB
 * 0x    10_0000 =   1 MB
 * 0x   100_0000 =  16 MB
 * 0x  1000_0000 = 256 MB
 * 0x1_0000_0000 =   4 GB
 */

KERNEL_STACK_SIZE = 64K;

/* 
 * The beginning of the Identity area will be BIOS stuff which may still be useful during kernel setup.
 * For example, the terminal graphics buffer.
 */


MEMORY 
{
    /* Nothing goes in the preface area! These all will be 4MB aligned to help with paging. */

    preface_area : ORIGIN = PREFACE_OFFSET, LENGTH = PREFACE_SIZE
    ro_area : ORIGIN = RO_OFFSET, LENGTH = RO_SIZE
    shared_data_area : ORIGIN = SHARED_DATA_OFFSET, LENGTH = SHARED_DATA_SIZE
    kernel_data_area : ORIGIN = KERNEL_DATA_OFFSET, LENGTH = KERNEL_DATA_SIZE
    user_data_area : ORIGIN = USER_DATA_OFFSET, LENGTH = USER_DATA_SIZE
}

/* Tell where the various sections of the object files will be put in the final
   kernel image. */
SECTIONS
{
	/* It used to be universally recommended to use 1M as a start offset,
	   as it was effectively guaranteed to be available under BIOS systems.
	   However, UEFI has made things more complicated, and experimental data
	   strongly suggests that 2M is a safer place to load. In 2016, a new
	   feature was introduced to the multiboot2 spec to inform bootloaders
	   that a kernel can be loaded anywhere within a range of addresses and
	   will be able to relocate itself to run from such a loader-selected
	   address, in order to give the loader freedom in selecting a span of
	   memory which is verified to be available by the firmware, in order to
	   work around this issue. This does not use that feature, so 2M was
	   chosen as a safer option than the traditional 1M. */

    .text : {
		*(.multiboot)
        *(.text)
    } > ro_area

    .rodata : {
        *(.rodata)
    } > ro_area

    /*
     * Other than during initialization, when the kernel thread is running, it will have a shared
     * data area and a kernel data area.
     *
     * When a user thread is running, it will have a shared data area and a user data area.
     *
     * The user code data which is initially loaded will be what is used for the first task.
     */

    /* Shared Sections */

    .data.shared : {
        libs_*.a:*.o(.data)
    } > shared_data_area

    COMMON.shared : {
        libs_*.a:*.o(.COMMON)
    } > shared_data_area

    .bss.shared : {
        libs_*.a:*.o(.bss)
    } > shared_data_area

    /* Kernel Sections */

    .data.kernel : {
        libk_*.a:*.o(.data)
    } > kernel_data_area

    COMMON.kernel : {
        libk_*.a:*.o(.COMMON)
    } > kernel_data_area

    .bss.kernel : {
        libk_*.a:*.o(.bss)
    } > kernel_data_area

    /* User sections */

    .data.user : {
        libu_*.a:*.o(.data)
    } > user_data_area

    COMMON.user : {
        libu_*.a:*.o(.COMMON)
    } > user_data_area

    .bss.user : {
        libu_*.a:*.o(.bss)
    } > user_data_area


    /* Stack should not be defined in the linker script! */

}
